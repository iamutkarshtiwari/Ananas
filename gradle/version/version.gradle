ext.versionFilePath = "${project.rootDir}/gradle/version/version.properties"

task bumpMajor {
    doLast {
        ant.propertyfile(file: versionFilePath) {
            entry(key: 'major', type: 'int', operation: '+', value: 1)
            entry(key: 'minor', type: 'int', operation: '=', value: 0)
            entry(key: 'patch', type: 'int', operation: '=', value: 0)
        }
    }
}

task bumpMinor {
    doLast {
        ant.propertyfile(file: versionFilePath) {
            entry(key: 'minor', type: 'int', operation: '+', value: 1)
            entry(key: 'patch', type: 'int', operation: '=', value: 0)
        }
    }
}

task bumpPatch {
    doLast {
        ant.propertyfile(file: versionFilePath) {
            entry(key: 'patch', type: 'int', operation: '+', value: 1)
        }
    }
}

def getVersionProps() {
    def versionPropsFile = file(versionFilePath)
    def props = new Properties()
    props.load(new FileInputStream(versionPropsFile))
    return props
}

ext.versionName = {
    def props = getVersionProps()
    return "${props['major']}.${props['minor']}.${props['patch']}"
}
